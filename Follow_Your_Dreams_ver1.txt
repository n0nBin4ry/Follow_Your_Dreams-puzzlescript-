title Follow Your Dreams [version2]
author Richard Flores
homepage https://github.com/rogue64

color_palette famicom



norepeat_action;

background_color #0a001f
text_color #8c4700
zoomscreen 15x10

========
OBJECTS
========

Background1 
#b3b300
background2
#262626

stopper1
#262626

flag
transparent

exit1
#8b0000 black
.111.
10001
11001
10001
11111
exit2
#8b0000 black
00100
00100
00100
00100
00100


gate1
#8b0000 black
.111.
10001
11001
10001
11111

Target 
red 

Wall 
#000080 black
00010
11111
01000
11111
00010
fakeWall 
#000080 black
00010
11111
01000
11111
00010
wallbackground
#0a001f
finalwall
#0a001f

ekey
grey
00000
0....
00000
0....
00000
xkey
grey
0...0
.0.0.
..0..
.0.0.
0...0
ikey
grey
00000
..0..
..0..
..0..
00000
tkey
grey
00000
..0..
..0..
..0..
..0..
dwn
grey
..0..
..0..
0.0.0
.0.0.
..0..


Player1 
darkBlue black
11111
10001
10001
10001
11111
player2
darkblue
.00..
0000.
.00..
0..0.
0..0.
player3right
black pink grey #9d9dff #000058
411.3
.44.3
00002
.00..
0..0.
player3left
black pink grey #9d9dff #000058
3.114
3.44.
20000
..00.
.0..0
player4right
#00cc66 pink grey #9d9dff #a52a2a brown
.00.3
011.3
55002
5544.
4..4.
player4left
#00cc66 pink grey #9d9dff #a52a2a brown
3.00.
3.110
20055
.4455
.4..4
player5right
#00cc66 pink black #8c0000 #a52a2a brown
.00.3
011.3
55002
5544.
4..4.
player5left
#00cc66 pink black #8c0000 #a52a2a brown
3.00.
3.110
20055
.4455
.4..4

deadplayer1
darkred black
11111
10001
10001
10001
11111
deadplayer2
darkred
.00..
0000.
.00..
0..0.
0..0.
deadplayer3right
darkred red grey brown black
411..
.44..
0000.
.00..
0..0.
deadplayer3left
darkred red grey brown black
..114
..44.
.0000
..00.
.0..0
deadplayer3rightblade
grey #9d9dff
.....
.....
.....
.....
011..
deadplayer3leftblade
grey #9d9dff
.....
.....
.....
.....
..110
deadplayer4right
darkred red grey #9d9dff #8b0000 brown
.00..
011..
0000.
4444.
4..4.
deadplayer4left
darkred red grey #9d9dff #8b0000 brown
..00.
..110
.0000
.4444
.4..4
deadplayer4rightshield
brown
.....
.....
.....
...00
...00
deadplayer4leftshield
brown
.....
.....
.....
00...
00...

Crate1 
Orange 
crate2
black grey
11111
01010
01010
01010
11111
crate3
black grey lightblue
00000
02120
01210
02120
00000


zombie1
darkgreen
scientist1
#c49c67 #add8e6 black white grey
.222.
.101.
33433
.343.
.4.4.
scientist2
green #add8e6 darkgreen white grey
.222.
.101.
33433
.343.
.4.4.

sentry1right
#800080 grey darkred black yellow orange
.0112
45000
50000
33033
33.33
sentry1up
#800080 grey darkred black yellow orange
.000.
00450
04500
30003
3...3
sentry1left
#800080 grey darkred black yellow orange
2110.
00004
00045
33033
33.33
sentry1down
#800080 grey darkred black #408000 #ff0000 
.121.
00500
00500
30503
3.5.3

laser1right
#ff0000
00000
.....
.....
.....
.....
laser1left
#ff0000
00000
.....
.....
.....
.....
laser1down
#ff0000
..0..
..0..
..0..
..0..
..0..
laser1up
#ff0000
..0..
..0..
..0..
..0..
..0..
laser1cross
#ff0000
00000
..0..
..0..
..0..
..0..

gold
yellow orange #262626
..01.
.001.
00001
00001
00000
gold2
yellow orange #262626
..01.
.001.
00001
00001
00000

annoyingguard1
red
annoyingguard1blood
blue #262626
10111
11001
10000
10000
01101
annoyingguard2
blue
annoyingguard2blood
red #262626
10111
11001
10000
10000
01101

mom
#ffc0cb #ff748c pink black
.333.
32223
11011
.101.
.000.
momwin
black #ff748c darkred black
.333.
32223
11011
.101.
.000.

warp1
lightblue black
10111
10000
10101
00001
11101

root1
brown darkbrown
01001
10100
10100
01001
01001
root1r
brown darkbrown
0....
00...
000..
11000
00101
root1l
brown darkbrown
....0
...00
..000
.0011
10100
tree1
brown darkbrown
01001
10100
10100
01001
01001
tree2
brown darkbrown
01001
10100
10100
01001
01001
tree3
brown darkbrown
01001
10100
10100
01001
01001
tree4
green darkgreen #0a001f
01110
00000
00000
01001
10010
tree4r
green darkgreen #0a001f
00000
11100
00000
00110
00000
tree4l
green darkgreen #0a001f
00000
11100
00000
00110
00000
tree4r2
green darkgreen #0a001f
11112
00001
01001
00001
10101
tree4l2
green darkgreen #0a001f
21111
10000
10010
10000
10101
tree3r
green darkgreen #0a001f
00101
00001
01001
01012
22222
tree3l
green darkgreen #0a001f
10100
10000
10010
21010
22222
tree5
green darkgreen #0a001f
21112
10001
10001
00100
00010
tree5r
green darkgreen #0a001f
22222
22222
11112
00001
00001
tree5l
green darkgreen #0a001f
22222
22222
21111
10000
10000
tinyroot1
brown darkbrown #0a001f
.001.
.011.
.001.
.011.
.001.
tinytree1
brown darkbrown #0a001f
20012
20112
20012
20112
20012
tinytree2
green darkgreen #0a001f
21112
10001
10001
10001
21112

gigaslashSheathe
#8c0000 black white orange pink
.....
..133
.303.
1303.
13333
gcal
orange #8c0000 black
..22.
..10.
00110
.1010
.0..1

randomFriend
transparent
friend1
pink darkred yellow darkblue
.111.
.000.
22222
23332
.0.0.
zfriend1
green darkred yellow darkblue
.111.
.000.
22222
23332
.0.0.
friend2
pink black white darkblue
.111.
.000.
22222
23332
.3.3.
zfriend2
green black white darkblue
.111.
.000.
22222
23332
.3.3.
friend3
pink black red black
.111.
10001
22222
23332
.3.3.
zfriend3
green black red black
.111.
10001
22222
23332
.3.3.

bloodsplat
#8c0000
.0...
..00.
.0000
.0000
0..0.

checkpoint1
transparent
checkpoint2
transparent

sentryscanner
transparent
goldscanner
transparent

sentrydeath1
transparent
sentrydeath2
transparent
sentrydeath3
transparent
sentrydeath4
transparent
sentrydeath5
transparent
sentrydeath6
transparent
sentrydeath7
transparent

initmessage
transparent
message1
transparent
message2
transparent
message3
transparent
message4
transparent
message5
transparent
endflag
transparent

=======
LEGEND
=======
& = stopper1
[ = gate1
f = flag
. = Background1
, = background2
# = Wall
! = fakewall
` = wallbackground
9 = finalwall
1 = Player1
2 = player2
3 = player3right
4 = player4right and background2
* = Crate1
+ = crate2
" = crate3 and background2
@ = Crate1 and Target
O = Target
z = zombie1
s = scientist1
} = exit1
{ = exit2 and background2
E = ekey and wall
X = xkey and wall
I = ikey and wall
T = tkey and wall
- = dwn and wall
k = sentry1right and background2
h = sentry1left and background2
j = sentry1down and background2
u = sentry1up and background2
~ = gold and background2
g = gigaslashSheathe
c = checkpoint1 and background2
v = checkpoint2
$ = sentryscanner
? = goldscanner
a = annoyingguard1
b = annoyingguard2 
d = mom
m = initmessage and background2
n = initmessage
w = warp1 and initmessage
r = root1
y = tinyroot1
: = randomfriend


sentry = sentry1right or sentry1up or sentry1left or sentry1down
background = background1 or background2
exit = exit1 or flag or exit2
crate = crate1 or crate2 or crate3
player3 = player3right or player3left
player4 = player4right or player4left
player5 = player5right or player5left

player = player1 or player2 or player3 or player4 or player5
enemies = zombie1 or scientist1 or scientist2
dead = deadplayer1 or deadplayer2 or deadplayer3right or deadplayer3left or deadplayer3rightblade or deadplayer3leftblade or deadplayer4right or deadplayer4left or deadplayer4rightshield or deadplayer4leftshield
sign = Ekey or Xkey or Ikey or Tkey or dwn
laser1hor = laser1right or laser1left
laser1vert = laser1down or laser1up
laser = laser1hor or laser1vert or laser1cross
gate = gate1
scanners = sentryscanner or goldscanner
annoyingguards = annoyingguard1 or annoyingguard2 
npc = annoyingguards or mom or momwin
sentrydeath = sentrydeath1 or sentrydeath2 or sentrydeath3 or sentrydeath4 or sentrydeath5 or sentrydeath6 or sentrydeath7
messages = sentrydeath or initmessage or message1 or message2 or message3 or message4 or message5
warps = warp1
walls = wall or fakewall or wallbackground
blocker = walls or crate
obstacle = blocker or sentry or stopper1
trees = root1 or root1r or root1l or tree1 or tree2 or tree3 or tree4 or tree4r or tree4l or tree4r2 or tree4l2 or tree3r or tree3l or tree5 or tree5r or tree5l or tinyroot1 or tinytree2 or tinytree1
george = gigaslashSheathe or gcal
friends = friend1 or friend2 or friend3
zfriends = zfriend1 or zfriend2 or zfriend3

=======
SOUNDS
=======

showmessage 43979706
sfx1 43979706
sfx2 39624702
sfx3 65686109 (knocking)
sfx4 48673902 (friend death)
sfx5 37557302 (enemies death)
gold destroy 92886900
gold2 destroy 92886900

================
COLLISIONLAYERS
================

Background
Target, exit, gate, stopper1, checkpoint1, warps, checkpoint2, bloodsplat, annoyingguard1blood, annoyingguard2blood
Player, Crate, Walls, enemies, sentry, gold, npc, gold2, trees, george, randomfriend, friends, finalwall, zfriends
dead
sign, laser1cross
laser1hor
laser1vert
scanners, messages
endflag

======
RULES     
======     

[ >  Player | Crate ] -> [ > Player | > Crate ]   

[ > Player | walls ] -> CANCEL
[ > crate | walls ] -> CANCEL


[ left player3 ] -> [ left player3left ]  
[ right player3 ] -> [ right player3right ]
[ left player4 ] -> [ left player4left ]
[ right player4 ] -> [ right player4right ]
[ left player5 ] -> [ left player5left ]
[ right player5 ] -> [ right player5right ]

[> player | gold ] -> [ | player background2 ] message *Gold +1*
[> player | gold2 ] -> [ | player background2 ] message Gold +1

( "zombies" )

[ player | ... | enemies ] -> [ player | ... | < enemies ]
[ player | ... | zfriends ] -> [ player | ... | < zfriends ]
[ > enemies | ... | crate ] -> [ enemies | ... | crate ]

[ > enemies | gate ] -> [ enemies | gate ]

[ >  crate | enemies ] -> [ > crate | > enemies ]

late [ zombie1 | scientist1 ] -> [ zombie1 | scientist2 ]
late [ scientist2 | scientist1 ] -> [ scientist2 | scientist2 ]
late [ scientist2 | scientist1 ] -> [ scientist2 | scientist2 ]
[ scientist1 | ... | scientist2 ] -> [ scientist1 | ... | < scientist2 ]
late [ enemies | player1 ] -> [ enemies | deadplayer1 ]
late [ enemies | player2 ] -> [ enemies | deadplayer2 ]
late [ enemies | player3right | ] -> [ enemies | deadplayer3right | deadplayer3rightblade ] 
late [ enemies | player3left | ] -> [ enemies | deadplayer3left | deadplayer3leftblade ] 




(sentries and lasers.. my lord)

[ action player | sentry ][ sentrydeath7 ] -> [ player | ][ ] sfx2 message Just spare everyone else! They're my best friends! *Gold +1*
[ action player | sentry ][ sentrydeath6 ] -> [ player | ][ sentrydeath7 ] sfx2 message Why? *Gold +1*
[ action player | sentry ][ sentrydeath5 ] -> [ player | ][ sentrydeath6 ] sfx2 message Curse you! *Gold +1*
[ action player | sentry ][ sentrydeath4 ] -> [ player | ][ sentrydeath5 ] sfx2 message I was so excited for the new Gigaslash game. *Gold +1*
[ action player | sentry ][ sentrydeath3 ] -> [ player | ][ sentrydeath4 ] sfx2 message Beep-Boop. I feel no pain. Shutting down. *Gold +1*
[ action player | sentry ][ sentrydeath2 ] -> [ player | ][ sentrydeath3 ] sfx2 message I actually had a date tonight... *Gold +1*
[ action player | sentry ][ sentrydeath1 ] -> [ player | ][ sentrydeath2 ] sfx2 message Who will look after Droidina? *Gold +1*
[ action player | sentry ][ ] -> [ player | ][ sentrydeath1 ] sfx2 message Noooo! I was one week from retirement! *Gold +1*


[laser1vert laser1hor] -> [laser1cross] 
[ laser ] -> [ ]
late [ blocker laser ] -> [ blocker ] 

right [ sentry1right ] -> [right sentry1right ] 
left [ sentry1left ] -> [ left sentry1left ]
down [ sentry1down ] -> [ down sentry1down ]
up [ sentry1up ] -> [ up sentry1up ]

[ > sentry1right | obstacle ] -> [ sentry1down | obstacle ]
[ > sentry1down | obstacle ] -> [ sentry1left | obstacle ]
[ > sentry1left | obstacle ] -> [ sentry1up | obstacle ] 
[ > sentry1up | obstacle ] -> [ sentry1right | obstacle ]

right [ sentry1right | no blocker ] -> [ sentry1right | laser1right ]
left [ sentry1left | no blocker ] -> [ sentry1left | laser1left ]
down [ sentry1down | no blocker ] -> [ sentry1down | laser1down ]
up [ sentry1up | no blocker ] -> [ sentry1up | laser1up ]

late right [ Laser1right | no blocker ] -> [ laser1right | laser1right ]
late left [ Laser1left | no blocker ] -> [ laser1left | laser1left ]
late down [ laser1down | no blocker ] -> [ laser1down | laser1down ]
late up [ laser1up | no blocker ] -> [ laser1up | laser1up ]

late [ laser sentry ] -> [ sentry ] 

late right [ laser player3right | ] -> [ laser deadplayer3right | deadplayer3rightblade ] 
late left [ laser player3left | ] -> [ laser deadplayer3left | deadplayer3leftblade ] 

(game 9 stuff)

[ action player4 | annoyingguard1 | initmessage ] -> [ player4 | annoyingguard1 | background2 ] message HALT. I AM BLUEBOT. NO ONE GETS THROUGH, ESPECIALLY WEAKINGLINGS LIKE YOU. MAYBE MY SLIGHTLY LESS-STRONG BROTHER WILL LET YOU.

[ action player4 | annoyingguard2 | initmessage ] -> [ player4 | annoyingguard2 | background2 ] message umm, hey i'm redbot. yeah you can't get through.. lord gigaslash has deemed you unworthy. please walk away or me and my slightly less-strong brother will have to destroy you; unless maybe he lets you through.

[ > player4 | annoyingguard1 | initmessage ] -> [ player4 | annoyingguard1 | background2 ] message HALT. I AM BLUEBOT. NO ONE GETS THROUGH, ESPECIALLY WEAKINGLINGS LIKE YOU. MAYBE MY SLIGHTLY LESS-STRONG BROTHER WILL LET YOU.

[ > player4 | annoyingguard2 | initmessage ] -> [ player4 | annoyingguard2 | background2 ] message umm, hey i'm redbot. yeah you can't get through.. lord gigaslash has deemed you unworthy. please walk away or me and my slightly less-strong brother will have to destroy you; unless maybe he lets you through.

[ action player4 | annoyingguard1 | background2 ] -> [ player4 | annoyingguard1 | background2 ] message Bluebot: WHAT DID I SAY? WALK AWAY PUNY PERSON!

[ action player4 | annoyingguard2 | background2 ] -> [ player4 | annoyingguard2 | background2 ] message Redbot: what did i say? please walk away.




right [ sentryscanner | ... | no initmessage ] [ fakewall ] -> [ sentryscanner | ... | no initmessage ] [ warp1 ]
right [ sentryscanner | ... | warp1 ] [ sentryscanner | ... | initmessage ] -> [ sentryscanner | ... | fakewall ] [ sentryscanner | ... | initmessage ]

up [ sentryscanner | ... | no gigaslashsheathe ] [ mom ] -> [ sentryscanner | ... | no gigaslashsheathe ] [ momwin ]
up [ sentryscanner | ... | momwin ] [ sentryscanner | ... | gigaslashsheathe ] -> [ sentryscanner | ... | mom ] [ sentryscanner | ... | gigaslashsheathe ]

up [ sentryscanner | ... | gcal ] [ friend1 ] -> [ sentryscanner | ... | gcal ] [ zfriend1 ]
up [ sentryscanner | ... | gcal ] [ friend2 ] -> [ sentryscanner | ... | gcal ] [ zfriend2 ]
up [ sentryscanner | ... | gcal ] [ friend3 ] -> [ sentryscanner | ... | gcal ] [ zfriend3 ]
up [ sentryscanner | ... | initmessage | gcal ] -> [ sentryscanner | ... | | gcal ] message Lost-Soul: You just abandonded us.. for your own dreams! Left us to stay the same.. doing nothing. Maybe we could've helped.. maybe we could've shared your dream.

[action player5 | gcal ] -> [ player5 | gcal ] message Lost-Soul: It's too late now..




late right [ player warp1 | ... | warp1 | ] -> [ warp1 | ... | warp1 | player ] 
late left [ player warp1 | ... | warp1 | ] -> [ warp1 | ... | warp1 | player ] 
late down [ player warp1 | ... | warp1 | ] -> [ warp1 | ... | warp1 | player ] 
late up [ player warp1 | ... | warp1 | ] -> [ warp1 | ... | warp1 | player ]



([ orthogonal player4 | mom | message1 ] -> [ player4 | mom | message2 ] message No. They'll understand when I achieve my dreams.
late [ player4 | mom | initmessage ] -> [ player4 | mom | message1 ] message "Don't you miss your friends?")

[ action player4 | mom ] -> [ player4 | mom ] message "Don't forget to visit grandpa!"



[ player4 ][ message3 ] -> [ player4 ][ message5 ] message Yeah. I think I have enough time tomorrow. I'll drive by then.
[ player4 ][message2 ] -> [ player4 ][message3] message "Hey don't forget to go visit grandpa soon!"
late [ player4 ] [message1] -> [ player4 ] [message2] sfx3 message * Knock Knock *

late [ player4 checkpoint1 | initmessage ][] -> [ player4 checkpoint1 | ][message1] message + Ye Olde Friendly Towne + 
late [ player4 checkpoint2 ] -> [ player4 checkpoint2] message + Ye "Old Spot" + 



[ action player4 | mom | message2 ] [ no gold2 ] -> [ player4 | right mom | ] [ no gold2 ]
[ > mom | player ] -> [ > mom | > player ]


[ > enemies | player4right | ] -> [ enemies deadplayer4rightshield | deadplayer4right | deadplayer3rightblade] 
[ > enemies | player4left | ] -> [ enemies deadplayer3leftblade | deadplayer4left | deadplayer4leftshield ] 


up [tinyroot1 | ] -> [tinyroot1 | tinytree1]
up [tinytree1 | ] -> [tinytree1 | tinytree2]

up [ root1 | ] -> [root1 | tree1] 
right [root1| ] -> [ root1 | root1r ]
left [root1 | ] -> [ root1 | root1l ]
up [ tree1 | ] -> [tree1 | tree2 ] 
up [ tree2 | ] -> [tree2 | tree3 ] 
up [ tree3 | ] -> [tree3 | tree4 ] 
right [tree4 | ] -> [ tree4 | tree4r ]
right [ tree4r | ] -> [ tree4r | tree4r2 ]
left [tree4 | ] -> [tree4 | tree4l ]
left [ tree4l | ] -> [ tree4l | tree4l2 ]
right [tree3 | ] -> [ tree3 | tree3r ] 
left [tree3 | ] -> [ tree3 | tree3l ]
up [ tree4 | ] -> [ tree4 | tree5 ]
right [ tree5 | ] -> [ tree5 | tree5r ]
left [ tree5 | ] -> [ tree5 | tree5l ]

[randomfriend] -> [random friends]

[ action player4right | gigaslashSheathe ] -> [ player5right | gcal ] message You recieved the legendary Gigaslash Blade! Go kill anything in your way, you're invincible!
[ action player4left | gigaslashSheathe ] -> [ player5left | gcal ] message You recieved the legendary Gigaslash Blade! Go kill anything in your way, you're invincible!
[ action player5 | zfriends ] -> [ player5 | bloodsplat ] sfx4
[ action player5 | enemies ] -> [ player5 | bloodsplat ] sfx5
[ action player5 | annoyingguard1 ] -> [ player5 | annoyingguard1blood ] sfx5 message Bluebot: DAMN YOU PUNY PERSON! BROTHER!.. help me
[ action player5 | annoyingguard2 ] -> [ player5 | annoyingguard2blood ] sfx5 message Redbot: .. why would you do this? brother.. MAKE SURE LORD GIGASLASH MAKES HIM PAY!

(real win conditions and checkpoints)

[ player2 flag ] -> win
[ player checkpoint1 ] -> [player] checkpoint
[ player checkpoint2 ] -> [player] checkpoint

late [ player2 exit ] -> win
late [ player1 exit ] -> win

late left [ goldscanner | ... | gold ] [ player3 exit2 ] -> [ goldscanner | ... | gold ] [ player3 exit2 ] message Transmission: Are you dense?! You forgot the gold!
late left [ sentryscanner | ... | sentry ] [ player3 exit2 ] -> [ sentryscanner | ... | sentry ] [ player3 exit2 ] message Transmission: Yo the boss says the droids are also made of gold. Smash 'em, yeah? They won't feel a thing. Don't forget that money is EVERYTHING!
late [ player3 exit ] -> win

up [ sentryscanner | ... | gold2 ][ ] -> [ sentryscanner | ... | gold2 ][ endflag ]

[ > player5 | finalwall ] -> [player5 | finalwall ] sfx3 win

(change of plans, no longer a win condition)
[ action player | momwin ] -> [player | momwin] message "Okay it's tomorrow! Why haven't you gone to visit him yet?"



==============
WINCONDITIONS
==============

All Target on Crate
no enemies

=======     
LEVELS
=======

message "Don't kid yourself."
message "You can't just become a game developer. You need to work."
message I know.
message Game 1: Push Crate on Target

#########
#.......#
#.....@.#
#.1.*.O.#
#.......#
#.......#	
#########

message "Wow, nice. You actually did it."
message "Now make more."
message Game 2: Escape Zombies (using crate to hide)

########E#X#I#T-#
#............z.}#
#.1.z.zzzz..z...#
#...*.****...*..#
#.............*z#
#################

message "Just blocks and letters?"
message Game 3: Not Just Blocks and Letters

###########E#X#I#T-#
#s................}#
#....2..........s..#
#......+......s....#
#..................#
####################

message "This is just the same game, but prettier.. kinda."
message "I meant like what else is there besides a puzzle. What's the theme? Or moral?"
message Game 4: Theme or Moral?

################
#zssz..fff.szss#
#sss...f2f...sz#
#szs...fff.+sss#
#z.z.......szss#
#####E#X.I#T####
########z#######
########}#######

message "But don't worry about that though, buddy. You haven't gone out with us in forever."
message "We were thinking of going to the old spot and figuring out what movie to go see. Or just hang and be stupid kids. Wanna come?"
message No. I need to work.
message Game 7: Make Money, No Matter What
message Use the Spacebar to attack guards and/or pass time

#######################################
#######################################
###############&,,,,,&j&,,,,##########$
###############k&,,,,,&,,,,,##########$
....z################,,,##,,###########
...s.###########,,####,###,c{##########
....zE#######&##,,####,################
.....###k,,,,,&c",,,,,,,,,&j##########$
....sX#######&##,,,,,,,,,,,&###########
.....########,##,,,,,,,,,,,,###########
..z.zI#######,########,################
...s.########,######k,,,,,,,,j########$
.z...T,,,,,,,,######,,~,~,~,~,########?
s.s.3[,,,,,,,,######u,,,,,,,,h########$
...s.##################################

message How's that for a moral huh?
message But man that was a bitch to code. Took forever but just another addition to the portfolio.
message * buzz * * buzz * : 1 New Message
message "Hey man, I don't want to be the one to tell you but.. Do you know?"
message * type type type * : Know what? I have no idea what you're talking about.
message * buzz * * buzz * : "Shit man.. it's George.."
message - One Week Later -
message "I just can't fucking believe it man.. no note.. no warning.. no nothing."
message Yeah.
message "Why didn't he reach out, dammit.. Maybe he did.. I dunno."
message Yeah.
message ... (Maybe I could've known. Maybe he was reaching out before our fight about me always working...)
message "I couldn't believe that was him in there.. Still looked like he was gonna get up and go 'PSYCHE!' ya'know?"
message Yeah.
message "When you going to the reception?"
message I don't think I will. I need to work.
message "Still? For sure.. Hey I think you should put him in one of those games.. He'd like that."
message .. Yeah.
message Game 9: Gigaslash Chronicles 2
message Press Spacebar to talk and/or pass time

````````````````````````````````````````````````````````
````````````````````````````````````````````````````````
````````````````````````````````````````````````````````
````````````````````````````````````````````````````````
````````````````````````````````````````````````````````
````````````````````````````````````````.y.y.y....r..```
````````````````````````````````````````....:.....g..```
````````````````````````````````````````..:...:...n..```
````````````````````````````````````````...:.:.......```
```````.z................,..````````````.```````````````
```````.s.s.............,w,.```````````wv```````````````
```````.s.s...#..........,..````````````````````````````
```````.s.s.*..##...........````````````````````````````
```````#####..*...#.########````````````````````````````
```````.......######k,,,,,j#````````````````````````````
```````d....m#######,,~,~,,#````````````````````````````
```````....,c##{####u,,,,,h#````````````````````````````
````````````w```````````````````````````````````````````
````````````````````````````````````````````````````````
````````````````````````````````````````````````````````
````````````````````````````````````````````````````````
.......$....................``````````````````````$`````
............................````````````````````````````
............................````````````````````````````
............................````````````````````````````
$......#####!#########......````````````````````````````
.......#,,,,,,,,,,,,,#......````````````````````````````
.......#,,,,,,,,,,,,,#......````````````````````````````
.......#,,,,,,,,,,,,,#######````````````````````````````
$......#,,,,,,,,,,,,,,,,,,am9```````````````````````````
$......#,,,,,,4,,,,,,,,,,,bm9```````````````````````````
.......#,,,,,,,,,,,,,#######````````````````````````````
.......#,,,,,,,,,,,,,#......````````````````````````````
.......#,,,,,,,,,,,,,#z.....````````````````````````````
.......###############......````````````````````````````
............................````````````````````````````
............................````````````````````````````
............................````````````````````````````

message *Knock Knock*
message "Hey its time to go... We gotta say bye to grandpa."
message Bye? He was doing good wasn't he? I thought I had more time to work before paying him a visit..
message "We thought so too.."
message - One Infinite Car-Ride Later -
message "He's pumped with morphine so he will be comfortable.. But we can't wake him up."
message "He said this was for you though."
message .. (For me? He has never written to me before.)
message "If you're reading this then I guess I wasn't strong enough to wake up from surgery huh?"
message "Well I just want to let you know I'm so proud of you for chasing your dreams and working so hard."
message "Please be happy. And hey, go make more money than I ever could. Sorry I won't be there to help you count it."
message "Love, Grandpa."
message ...
message - Follow Your Dreams -
message Dedicated to Abuelito David and G-Cal

(debugging playground)
(
####################
#,,,,,,,,,,,,,,,,,,#
#,,,,,,,,,u,,,,,,,,#
#,,,,,,,,hjk,,#,f,,#
#,,,3,,#,,,,,,,,,,,#
#,,,,,,k,,,,,,,,,,,#
####################

...........................
...........................
...........................
...........................
......###############......
......#..:..........#......
......#..:..........#......
......#..:...:......#......
......#..:..........#......
......#...3....y.r..#......
......#.............#......
......#.............#......
......#.............#z.....
......###############......
...........................
...........................
...........................)

